apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        applicationId "sasd97.java_blog.xyz.yandexweather"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.code
        versionName versions.name
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {
            storeFile file(keystoreProperties['debugFile'])
            keyAlias keystoreProperties['debugAlias']
            keyPassword keystoreProperties['debugKeyPassword']
            storePassword keystoreProperties['debugStorePassword']
        }

        release {
            storeFile file(keystoreProperties['releaseFile'])
            keyAlias keystoreProperties['releaseAlias']
            keyPassword keystoreProperties['releaseKeyPassword']
            storePassword keystoreProperties['releaseStorePassword']
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }

        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    //tests
    androidTestCompile(libraries.espressoCore, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile libraries.junit

    //support library
    compile libraries.supportDesign
    compile libraries.supportAppCompat
    compile libraries.supportConstraint

    //http
    compile libraries.retrofit
    compile libraries.retrofitGson

    //moxy
    compile libraries.moxy
    compile libraries.moxyAppCompat
    annotationProcessor libraries.moxyCompiler

    //butterknife
    compile libraries.butterKnife
    annotationProcessor libraries.butterKnifeCompiler

    //dagger
    compile libraries.dagger
    annotationProcessor libraries.daggerCompiler

    //rx
    compile libraries.rxjava
    compile libraries.rxandroid
    compile libraries.rxadapter
}
